plugins {
    id 'java'
    id 'eclipse'
}

sourceCompatibility = 1.8
version = "1.0." + ('git rev-list --count HEAD'.execute().text.trim())
group = "fr.maxyolo01"
archivesBaseName = "BteFranceUtils"

repositories {
    mavenCentral()

    maven {
        // Spigot
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }

    maven {
        // Chat API
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        // Chat API
        url = 'https://oss.sonatype.org/content/repositories/central'
    }

    maven {
        // WorldEdit
        url = 'https://maven.enginehub.org/repo'
    }

    maven {
        // DiscordSRV
        url = 'https://nexus.scarsz.me/content/groups/public'
    }

    maven {
        // DiscordSRV (JDA)
        url = 'https://m2.dv8tion.net/releases'
    }
}

dependencies {

    implementation 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    implementation 'com.sk89q.worldedit:worldedit-core:6.1'
    implementation 'com.sk89q.worldedit:worldedit-bukkit:6.1.5'
    implementation 'com.discordsrv:discordsrv:1.23.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'

}

tasks.named('test') {
    useJUnitPlatform()
}


processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
        duplicatesStrategy = 'include'
    }
}
