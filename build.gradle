plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
    id 'eclipse'
}

sourceCompatibility = 1.8
version = "1.0." + ('git rev-list --count HEAD'.execute().text.trim())
group = "fr.maxyolo01"
archivesBaseName = "BteFranceUtils"

repositories {
    mavenCentral()

    maven {
        // Spigot
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }

    maven {
        // Chat API
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        // Chat API
        url = 'https://oss.sonatype.org/content/repositories/central'
    }

    maven {
        // WorldEdit
        url = 'https://maven.enginehub.org/repo'
    }

    maven {
        // DiscordSRV
        url = 'https://nexus.scarsz.me/content/groups/public'
    }

    maven {
        // DiscordSRV (JDA)
        url = 'https://m2.dv8tion.net/releases'
    }

    maven {
        // Get stuff from GitHub
        name = "JitPack"
        url = "https://jitpack.io/"
    }

    maven {
        // Terra++ dependencies
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        // Terra++ dependencies
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/snapshot/"
    }
}

dependencies {

    implementation 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    implementation ('com.github.BTE-France:BTE-France-WorldEdit:2eb559cd14') {
        exclude module: 'worldedit-sponge-api7.1.0'
        exclude module: 'worldedit-forge-mc1.12.2'
    }
    implementation 'com.discordsrv:discordsrv:1.23.0'
    implementation 'com.github.BuildTheEarth:terraplusplus:a48a5a2054f4655d230711c3c68af3ad3b476db4'
    implementation 'fr.dudie:nominatim-api:3.4'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation 'org.junit-pioneer:junit-pioneer:0.1.2'
    testImplementation 'com.github.BuildTheEarth:terraplusplus:a48a5a2054f4655d230711c3c68af3ad3b476db4'

}

build.dependsOn shadowJar

shadowJar {
    archiveClassifier.set('')
    dependencies {
        include(dependency('fr.dudie:nominatim-api:3.4'))
        include(dependency {
            it.moduleGroup == 'org.apache.httpcomponents'
                    || it.moduleGroup == 'commons-logging'
                    || it.moduleGroup == 'com.github.filosganga'
                    || it.moduleGroup == 'org.locationtech.jts'
                    || it.moduleGroup == 'com.vividsolutions'
        })
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


processResources {
    inputs.property "version", project.version
    duplicatesStrategy = 'include'

    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}